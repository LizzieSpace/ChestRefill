import com.modrinth.minotaur.TaskModrinthUpload

plugins {
    id "com.github.johnrengelman.shadow" version "7.0.0"

    id 'com.matthewprenger.cursegradle'
    id "com.modrinth.minotaur"
}

def ENV = System.getenv();
architectury {
    platformSetupLoomIde()
    forge()
}

loom {
    forge {
        mixinConfigs "chestrefill.common.mixins.json"
    }
}

configurations {
    common
    shadowCommon // Don't use shadow from the shadow plugin because we don't want IDEA to index this.
    compileClasspath.extendsFrom common
    runtimeClasspath.extendsFrom common
    developmentForge.extendsFrom common
}

dependencies {
    forge "net.minecraftforge:forge:${rootProject.minecraft_version}-${rootProject.forge_version}"

    // C2B lib
    modImplementation "com.github.samolego.Config2Brigadier:config2brigadier-forge:${rootProject.c2b_version}"
    shadowCommon("com.github.samolego.Config2Brigadier:config2brigadier-forge:${rootProject.c2b_version}") {
        transitive = false
    }


    common(project(path: ":common", configuration: "dev")) {
        transitive = false
    }
    shadowCommon(project(path: ":common", configuration: "transformProductionForge")) {
        transitive = false
    }
}

processResources {
    inputs.property "version", project.version
    inputs.property "c2b_version", rootProject.c2b_version

    filesMatching("META-INF/mods.toml") {
        expand("version": project.version,
               "c2b_version": rootProject.c2b_version)
    }
}

shadowJar {
    exclude "fabric.mod.json"

    configurations = [project.configurations.shadowCommon]
    classifier "dev-shadow"
}

remapJar {
    input.set shadowJar.archiveFile
    dependsOn shadowJar
    classifier "forge"
}

jar {
    classifier "dev"
}

sourcesJar {
    def commonSources = project(":common").sourcesJar
    dependsOn commonSources
    from commonSources.archiveFile.map { zipTree(it) }
}

components.java {
    withVariantsFromConfiguration(project.configurations.shadowRuntimeElements) {
        skip()
    }
}

publishing {
    publications {
        mavenForge(MavenPublication) {
            artifactId = rootProject.archives_base_name + "-" + project.name
            from components.java
        }
    }

    // See https://docs.gradle.org/current/userguide/publishing_maven.html for information on how to set up publishing.
    repositories {
        // Add repositories to publish to here.
    }
}

curseforge {
    if (ENV.CURSEFORGE_API_KEY) {
        apiKey = ENV.CURSEFORGE_API_KEY
    }

    project {
        id = "536977"
        changelog = ENV.CHANGELOG ?: "A changelog can be found at https://github.com/samolego/ChestRefill/releases/tag/${version}"
        releaseType = "release"
        addGameVersion project.minecraft_version
        addGameVersion "Forge"

        mainArtifact(file("${project.buildDir}/libs/${archivesBaseName}-${version}-forge.jar")) {
            displayName = "[${project.minecraft_version}] ChestRefill ${version} [Forge]"
        }

        afterEvaluate {
            uploadTask.dependsOn("remapJar")
        }
    }

    options {
        forgeGradleIntegration = false
    }
}

tasks.modrinth {
    onlyIf {
        ENV.MODRINTH_TOKEN
    }
}

modrinth {
    token = ENV.MODRINTH_TOKEN
    projectId = "sv1q6VzG"
    versionNumber = project.minecraft_version + "+" + version + "-forge"
    changelog = ENV.CHANGELOG ?: "A changelog can be found at https://github.com/samolego/ChestRefill/releases/tag/${version}"
    versionName = "[${project.minecraft_version}] ChestRefill ${version} [Forge]"
    versionType = "release"

    uploadFile = file("${project.buildDir}/libs/${archivesBaseName}-${version}-forge.jar")

    gameVersions = [project.minecraft_version]
    loaders = ['forge']
}
